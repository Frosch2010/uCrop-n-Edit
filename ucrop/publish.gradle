apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Image Cropping + Editing Library for Android'
                    url = 'https://github.com/jens-muenker/uCrop-n-Edit'

                    licenses {
                        license {
                            name = 'uCrop\'n\'Edit License'
                            url = 'https://github.com/jens-muenker/uCrop-n-Edit'
                        }
                    }

                    developers {
                        developer {
                            name = 'Martin Anderson'
                            email = 'martins.andersons@hotmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/jens-muenker/uCrop-n-Edit.git'
                        developerConnection = 'scm:git:ssh://github.com/jens-muenker/uCrop-n-Edit.git'
                        url = 'https://github.com/jens-muenker/uCrop-n-Edit/'
                    }
                }
            }
        }
    }
}

/*signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}*/